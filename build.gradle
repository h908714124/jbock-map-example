plugins {
    id 'com.github.johnrengelman.shadow' version '5.1.0'
    id "application"
}

sourceCompatibility = '1.8'

mainClassName = "de.h90.example.Main"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.9'
    compileOnly "com.github.h908714124:jbock-annotations:2.2"
    annotationProcessor "com.github.h908714124:jbock:2.7.1"
}

jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
}

compileJava {
    options.compilerArgs << "-s"
    options.compilerArgs << "$projectDir/src/main/generated/java"

    doFirst {
        // make sure that directory exists
        file(new File(projectDir, "/src/main/generated/java")).mkdirs()
    }
}

clean.doLast {
    // clean-up directory when necessary
    file(new File(projectDir, "/src/main/generated")).deleteDir()
}

sourceSets {
    generated {
        java {
            srcDir "$projectDir/src/main/generated/java"
        }
    }
}
